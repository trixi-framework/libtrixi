program simple_trixi_controller_f
  use LibTrixi
  use, intrinsic :: iso_fortran_env, only: error_unit
  use, intrinsic :: iso_c_binding, only: c_int, c_null_char

  implicit none

  integer(c_int) :: handle
  character(len=256) :: argument

  if (command_argument_count() < 1) then
    call get_command_argument(0, argument)
    write(error_unit, '(a)') "ERROR: missing arguments: PROJECT_DIR LIBELIXIR_PATH"
    write(error_unit, '(a)') ""
    write(error_unit, '(3a)') "usage: ", trim(argument), " PROJECT_DIR LIBELIXIR_PATH"
    stop 1
  else if (command_argument_count() < 2) then
    call get_command_argument(0, argument)
    write(error_unit, '(a)') "ERROR: missing argument: LIBELIXIR_PATH"
    write(error_unit, '(a)') ""
    write(error_unit, '(3a)') "usage: ", trim(argument), " PROJECT_DIR LIBELIXIR_PATH"
    stop 1
  end if

  ! Initialize Trixi
  call get_command_argument(1, argument)
  call trixi_initialize(argument)

  ! Print version information
  write(*, '(a, i1, a, i1, a, i1, a, a)') "libtrixi version: ", &
        trixi_version_library_major(), ".", trixi_version_library_minor(), ".", &
        trixi_version_library_patch(), " ", trixi_version_library()
  write(*, '(a)') ""
  write(*, '(a)') "All loaded julia packages"
  write(*, '(a)') trixi_version_julia_extended()
  write(*, '(a)') ""

  ! Execute julia code
  write(*, '(a)') "Execute julia code"
  call trixi_eval_julia('println("3! = ", factorial(3))');

  ! Set up the Trixi simulation
  ! We get a handle to use subsequently
  call get_command_argument(2, argument)
  handle = trixi_initialize_simulation(argument)

  ! Get time step length
  write(*, '(a, e14.8)') "Current time step length: ", trixi_calculate_dt(handle)

  ! Main loop
  do
    ! Exit loop once simulation is completed
    if ( trixi_is_finished(handle) ) exit

    call trixi_step(handle)
  end do

  ! Finalize Trixi simulation
  call trixi_finalize_simulation(handle)

  ! Finalize Trixi
  call trixi_finalize()
end program
